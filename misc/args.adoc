[[auto-implicit-arguments]]
Auto implicit arguments
~~~~~~~~~~~~~~~~~~~~~~~

We have already seen implicit arguments, which allows arguments to be omitted when they can be inferred by the type checker, e.g.

----
index : {a:Type} -> {n:Nat} -> Fin n -> Vect n a -> a
----

In other situations, it may be possible to infer arguments not by type checking but by searching the context for an appropriate value, or constructing a proof.
For example, the following definition of `head` which requires a proof that the list is non-empty:

----
isCons : List a -> Bool
isCons [] = False
isCons (x :: xs) = True

head : (xs : List a) -> (isCons xs = True) -> a
head (x :: xs) _ = x
----

If the list is statically known to be non-empty, either because its value is known or because a proof already exists in the context, the proof can be constructed automatically.
Auto implicit arguments allow this to happen silently.
We define `head` as follows:

----
head : (xs : List a) -> {auto p : isCons xs = True} -> a
head (x :: xs) = x
----

The `auto` annotation on the implicit argument means that [logo]#Idris# will attempt to fill in the implicit argument using the `trivial` tactic, which searches through the context for a proof, and tries to solve with `refl` if a proof is not found.
Now when `head` is applied, the proof can be omitted.
In the case that a proof is not found, it can be provided explicitly as normal:

----
head xs {p = ?headProof} 
----

More generally, we can fill in implicit arguments with a default value by annotating them with `default`.
The definition above is equivalent to:

----
head : (xs : List a) -> 
       {default proof { trivial; } p : isCons xs = True} -> a
head (x :: xs) = x
----

[[implicit-conversions]]
Implicit conversions
~~~~~~~~~~~~~~~~~~~~

[logo]#Idris# supports the creation of __implicit conversions__, which allow automatic conversion of values from one type to another when required to make a term type correct.
This is intended to increase convenience and reduce verbosity.
A contrived but simple example is the following:

----
implicit intString : Int -> String
intString = show
  
test : Int -> String
test x = "Number " ++ x
----

In general, we cannot append an `Int` to a `String`, but the implicit conversion function `intString` can convert `x` to a `String`, so the definition of `test` is type correct.
An implicit conversion is implemented just like any other function, but given the `implicit` modifier, and restricted to one explicit argument.

Only one implicit conversion will be applied at a time.
That is, implicit conversions cannot be chained.
Implicit conversions of simple types, as above, are however discouraged! More commonly, an implicit conversion would be used to reduce verbosity in an embedded domain specific language, or to hide details of a proof.
Such examples are beyond the scope of this tutorial.
