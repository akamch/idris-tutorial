[[type-providers]]
Type Providers
~~~~~~~~~~~~~~

[logo]#Idris# type providers, inspired by F#â€™s type providers, are a means of making our types be ``about'' something in the world outside of Idris.
For example, given a type that represents a database schema and a query that is checked against it, a type provider could read the schema of a real database during type checking.

[logo]#Idris# type providers use the ordinary execution semantics of [logo]#Idris# to run an IO action and extract the result.
This result is then saved as a constant in the compiled code.
It can be a type, in which case it is used like any other type, or it can be a value, in which case it can be used as any other value, including as an index in types.

Type providers are still an experimental extension.
To enable the extension, use the `%language` directive:

----
%language TypeProviders
----

A provider `p` for some type `t` is simply an expression of type `IO (Provider t)`.
The `%provide` directive causes the type checker to execute the action and bind the result to a name.
This is perhaps best illustrated with a simple example.
The type provider `fromFile` reads a text file.
If the file consists of the string `Int`, then the type `Int` will be provided.
Otherwise, it will provide the type `Nat`.

----
strToType : String -> Type
strToType "Int" = Int
strToType _ = Nat

fromFile : String -> IO (Provider Type)
fromFile fname = do str <- readFile fname
                    return (Provide (strToType (trim str)))
----

We then use the `%provide` directive:

----
%provide (T1 : Type) with fromFile "theType"

foo : T1
foo = 2
----

If the file named `theType` consists of the word `Int`, then `foo` will be an `Int`.
Otherwise, it will be a `Nat`.
When [logo]#Idris# encounters the directive, it first checks that the provider expression `fromFile theType` has type `IO (Provider Type)`.
Next, it executes the provider.
If the result is `Provide t`, then `T1` is defined as `t`.
Otherwise, the result is an error.

Our datatype `Provider t` has the following definition:

----
data Provider a = Error String
                | Provide a
----

We have already seen the `Provide` constructor.
The `Error` constructor allows type providers to return useful error messages.
The example in this section was purposefully simple.
More complex type provider implementations, including a statically-checked SQLite binding, are available in an external collectionfootnote:[https://github.com/david-christiansen/idris-type-providers].
